# Resource: Create External DNS IAM Policy 
resource "aws_iam_policy" "externaldns_iam_policy" {
  count = var.enable_external_dns ? 1 : 0
  name  = "AlloExternalDNSUpdate"
  path  = "/"
  policy = jsonencode({
    "Version" : "2012-10-17",
    "Statement" : [
      {
        "Effect" : "Allow",
        "Action" : [
          "route53:ChangeResourceRecordSets"
        ],
        "Resource" : [
          "arn:aws:route53:::hostedzone/*" #---> or we can make it limited for only one hostedzone (arn:aws:route53:::hostedzone/Z44125962GM920SZ4S1E)
        ]
      },
      {
        "Effect" : "Allow",
        "Action" : [
          "route53:ListHostedZones",
          "route53:ListResourceRecordSets"
        ],
        "Resource" : [
          "*"
        ]
      }
    ]
  })
}
# Resource: Create IAM Role 
resource "aws_iam_role" "externaldns_iam_role" {
  count = var.enable_external_dns ? 1 : 0
  name  = "externaldns-iam-role"

  # Terraform's "jsonencode" function converts a Terraform expression result to valid JSON syntax.
  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRoleWithWebIdentity"
        Effect = "Allow"
        Sid    = ""
        Principal = {
          Federated = aws_iam_openid_connect_provider.eks.arn
        }
        Condition = {
          StringEquals = {
            "${replace(aws_iam_openid_connect_provider.eks.url, "https://", "")}:aud" : "sts.amazonaws.com",
            "${replace(aws_iam_openid_connect_provider.eks.url, "https://", "")}:sub" : "system:serviceaccount:kube-system:external-dns"
          }
        }
      },
    ]
  })
}

# Associate External DNS IAM Policy to IAM Role
resource "aws_iam_role_policy_attachment" "externaldns_iam_role_policy_attach" {
  count      = var.enable_external_dns ? 1 : 0
  policy_arn = aws_iam_policy.externaldns_iam_policy[0].arn
  role       = aws_iam_role.externaldns_iam_role[0].name
}

















